---
  title: "WriteDocument"
  library: "noflo-couchdb"
  layout: "component"

---

noflo = require "noflo"
{ CouchDbComponentBase } = require "../lib/CouchDbComponentBase"

# Ports:
#   In:   URL Inherited from CouchDbComponentBase parent class to receive connection information to CouchDB.
#             When a URL is received, the parent constructor will create an @dbConnection for us.
#         IN  Created in this class to receive document IDs to write to CouchDB
#
#   Out:  LOG Inherited from LoggingComponent to send log messages for error handling.
#         OUT Created in this class to send documents that were written to CouchDB.

class WriteDocument extends CouchDbComponentBase
  constructor: ->
    super
    @request = null
    @pendingRequests = []

    @inPorts.in = new noflo.Port()
    @outPorts.out = new noflo.Port()

    @inPorts.url.on "data", (data) =>
      if @dbConnection?
        @saveObject doc for doc in @pendingRequests
      else
        @sendLog
          logLevel: "error"
          context: "Connecting to the CouchDB database at URL '#{data}'."
          problem: "Parent class CouchDbComponentBase didn't set up a connection."
          solution: "Refer the document with this context information to the software developer."

    @inPorts.in.on "data", (doc) =>
      if @dbConnection
        @saveObject doc
      else
        @pendingRequests.push doc
        
    @inPorts.in.on "disconnect", =>
      @outPorts.out.disconnect()
      @outPorts.log.disconnect()

  saveObject: (object) =>
    @dbConnection.insert object, (err, response) =>
      if err?
        @sendLog
          logLevel: "error"
          context: "Writing a document to CouchDB."
          problem: err
          solution: "Resolve all conflicts and check that you have permission to insert a document into this database."
      else
        object.id = response.id unless object.id
        object.rev = response.rev
        @outPorts.out.send object if @outPorts.out.isAttached()

exports.getComponent = -> new WriteDocument
