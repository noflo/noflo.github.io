---
  title: "Set"
  library: "noflo-redis"
  layout: "component"

---
noflo = require 'noflo'
{RedisComponent} = require '../lib/RedisComponent.coffee'

class Set extends RedisComponent
  constructor: ->
    @key = null

    @inPorts =
      key: new noflo.Port
      value: new noflo.Port
    @outPorts =
      out: new noflo.Port
      error: new noflo.Port

    @inPorts.key.on 'data', (data) =>
      @key = data

    super 'value'

  doAsync: (value, callback) ->
    unless @redis
      callback new Error 'No Redis connection available'
      return

    unless @key
      callback new Error 'No key defined'
      return

    if typeof value is 'object'
      value = JSON.stringify value

    @redis.set @key, value, (err, reply) =>
      return callback err if err
      return callback new Error 'No value' if reply is null
      @outPorts.out.beginGroup @key
      @outPorts.out.send reply
      @outPorts.out.endGroup()
      @outPorts.out.disconnect()
      callback()

exports.getComponent = -> new Set
